begin model

# Model for IGF1 interaction with IGF1R used in Erickson, et al. (2018)

# References:
# Kiselyov VV, ..., De Meyts P (2009) Mol Syst Biol 5:243 [PMID:19225456]

begin parameters

# fraction of cell to be considered in a simulation
#   The scaling factor f should multiply volumes, copy numbers, 
#   and rate constants for zero-order processes.
f 1.0 # [=] dimensionless, 0<f<=1

# Avogadro constant
NA 6.02214e23 # [=] molecules per mol

# volume of extracellular fluid per cell
#   Assumptions:
#    - 15 cm culture dishes (cells 95% confluent)
#    - 1.2e5 cells per cm^2
#    - growth surface area of 140 cm^2
#    - 1.7e7 cells per dish
#    - 0.25 mL of medium per square cm of growth surface
#    - 35 mL of medium per dish
Vecf_default 2.1e-9*f # [=] L per cell

dilution 1.0
Vecf=dilution*Vecf_default

IGF1_hot_conc 24.0e-12 # [=] M (7 pM or 24 pM, p. 8 of Kiselyov et al., 2009)

IGF1_cold_conc 0 # [=] M

# copies of IGF1 per cell
IGF1_hot_total=IGF1_hot_conc*(NA*Vecf)
IGF1_cold_total=IGF1_cold_conc*(NA*Vecf)

IGF1R_total 20000*f # [=] copies per cell (two copies of IGF1R per dimer)

# Rate constants. 
a1_perMpers	a1_perMpers__FREE__		# /M/s
a1=a1_perMpers/(NA*Vecf)	# [=] /M/s converted to /(molecule/cell)/s
a2_perMpers	a2_perMpers__FREE__		# /M/s
a2=a2_perMpers/(NA*Vecf)	# [=] /M/s converted to /(molecule/cell)/s
# reverse rate constant for IGF1 (ds) interaction with IGF1R (S1)
#   Free IGF1 binds Site 1 in IGFR1.
d1	d1__FREE__	# [=] /s

# reverse rate constant for IGF1 (hs) interaction with IGF1R (S2)
#   Free IGF1 binds Site 2 in IGFR1.
d2	d2__FREE__		# [=] /s

# rate constants for ligand-mediated Site 1-Site 2 crosslinking/uncrosslinking reactions

# reverse rate constants (uncrosslinking reactions)
#   Release of ligand from Site 1
d1prime	d1prime__FREE__			
#   Release of ligand from Site 2
d2prime	d2prime__FREE__ 			

# forward rate constant (crosslinking reactions)
#   IGF1 tethered at Site 1 (Site 2) binds Site 2 (Site 1).
kcr kcr__FREE__ # [=] /s
a1prime=kcr	# [=] /s
a2prime=(a2*a1prime*d1*d2prime)/(a1*d1prime*d2)	# [=] /s [By applying the detailed balance constraint]


end parameters

begin molecule types

# ligand (insulin-like growth factor 1)
#  ds: dimerization surface (this site is not functional in IGF1, as it is in insulin)
#  hs: hexamerization surface
IGF1(ds,hs,label~hot~cold)

# receptor (IGF1 receptor)
#  S1: Site 1
#  S2: Site 2
#  C: cysteine residues involved in disulfide bonds of the receptor dimer
IGF1R(S1,S2,C)

end molecule types

begin seed species

IGF1(ds,hs,label~hot) IGF1_hot_total
IGF1(ds,hs,label~cold) IGF1_cold_total
IGF1R(S1,S2,C!0).IGF1R(S1,S2,C!0) 0.5*IGF1R_total

end  seed species

begin observables


Molecules IGF1_hot_bound IGF1(ds!+,hs,label~hot) \
                         IGF1(ds,hs!+,label~hot) \
                         IGF1R(S1!1).IGF1(ds!1,hs!2,label~hot).IGF1R(S2!2)

#Species IGF1_hot_free IGF1(ds,hs,label~hot)
#Molecules IGF1_cold_bound IGF1(ds!+,hs,label~cold) \
#                         IGF1(ds,hs!+,label~cold) \
#                         IGF1R(S1!1).IGF1(ds!1,hs!2,label~cold).IGF1R(S2!2)
#Species IGF1_cold_free IGF1(ds,hs,label~cold)

end observables

begin functions

# B()=IGF1_hot_bound+IGF1_cold_bound
# B_div_F()=B()/(IGF1_hot_free+IGF1_cold_free)

end functions


begin reaction rules

# Free IGF1 reversibly binds a free copy of Site 1 in dimeric IGF1R.
IGF1(ds,hs)+IGF1R(S1,C!0).IGF1R(S2,C!0)<->IGF1(ds!1,hs).IGF1R(S1!1,C!0).IGF1R(S2,C!0) a1,d1
#   This rules stipulates that ligand binds S1 in R1 only if S2 in R2 is free, because
#   a steric constraint prevents simultaneous occupation of S1 in R1 and S2 in R2 by two different ligands. 
#   In other words, the ligand binding pocket comprising S1 in R1 and S2 in R2 can only accomodate a single ligand.
# Free IGF1 reversibly binds a free copy of Site 2 in dimeric IGF1R.

IGF1(ds,hs)+IGF1R(S2,C!0).IGF1R(S1,C!0)<->IGF1(ds,hs!1).IGF1R(S2!1,C!0).IGF1R(S1,C!0) a2,d2
#   This rules stipulates that ligand binds S2 in R1 only if S1 in R2 is free, because
#   a steric constraint prevents simultaneous occupation of S2 in R1 and S1 in R2 by two different ligands. 
#   In other words, the ligand binding pocket comprising S2 in R1 and S1 in R2 can only accomodate a single ligand.

# IGF1 bound at Site 1 in R1 reversibly crosslinks Site 1 in R1 and Site 2 in R2. 
#   Crosslinking is contingent on absence of a crosslink connecting Site 2 in R1 and Site 1 in R2.
IGF1R(S1!1,C!0).IGF1R(S1,S2,C!0).IGF1(ds!1,hs)<->\
IGF1R(S1!1,C!0).IGF1R(S1,S2!2,C!0).IGF1(ds!1,hs!2) a2prime,d2prime
IGF1R(S1!1,S2,C!0).IGF1R(S1!+,S2,C!0).IGF1(ds!1,hs)<->\
IGF1R(S1!1,S2,C!0).IGF1R(S1!+,S2!2,C!0).IGF1(ds!1,hs!2) a2prime,d2prime

# IGF1 bound at Site 2 in R1 reversibly crosslinks Site 2 in R1 and Site 1 in R2. 
#   Crosslinking is contingent on absence of a crosslink connecting Site 1 in R1 and Site 2 in R2.
IGF1R(S2!1,C!0).IGF1R(S1,S2,C!0).IGF1(ds,hs!1)<->\
IGF1R(S2!1,C!0).IGF1R(S1!2,S2,C!0).IGF1(ds!2,hs!1) a1prime,d1prime
IGF1R(S1,S2!1,C!0).IGF1R(S1,S2!+,C!0).IGF1(ds,hs!1)<->\
IGF1R(S1,S2!1,C!0).IGF1R(S1!2,S2!+,C!0).IGF1(ds!2,hs!1) a1prime,d1prime

end reaction rules

end model

# actions

generate_network({overwrite=>1})

saveConcentrations("t=0")

# dissociation rate experiments

# Hot ligand concentrations
# 7 pM = 8852 copies per cell used in steady state experiments
# 24 pM = 24.0e-12*(NA*Vecf)=30350 copies per cell used in dissociation rate experiments

setConcentration("IGF1(ds,hs,label~hot)",30350)

# incubate with hot ligand for two hours
simulate({suffix=>"incubate",method=>"ode",t_start=>0,t_end=>7200,n_steps=>10})

# wash hot ligand
setConcentration("IGF1(ds,hs,label~hot)",0.0)
setConcentration("IGF1(ds,hs,label~cold)","IGF1_cold_conc*(NA*Vecf)") # this is needed so connection is maintained

saveConcentrations("start_competition")

# 20 min fit from Fig 5D
parameter_scan({suffix=>"example7_1",parameter=>"IGF1_cold_conc",par_scan_vals=>[\
		2.3e-10,\
		5.1e-10,\
		1.2e-09,\
		2.8e-09,\
		6.6e-09,\
		1.5e-08,\
		3.9e-08,\
		8.7e-08,\
		2.2e-07,\
		4.9e-07,\
		],\
    method=>"ode",t_start=>0,t_end=>1200,n_steps=>120,print_functions=>1,reset_conc=>1})

resetConcentrations("start_competition")

# 60 min fit from Fig 5D
parameter_scan({suffix=>"example7_2",parameter=>"IGF1_cold_conc",par_scan_vals=>[\
		2.3e-10,\
		5.1e-10,\
		1.2e-09,\
		2.8e-09,\
		6.6e-09,\
		1.5e-08,\
		3.9e-08,\
		8.7e-08,\
		2.2e-07,\
		4.9e-07,\
		],\
    method=>"ode",t_start=>0,t_end=>3600,n_steps=>360,print_functions=>1,reset_conc=>1})

# steady state dose response experiment
resetConcentrations("t=0")

# 7 pM = 8852 copies per cell
setConcentration("IGF1(ds,hs,label~hot)",8852)
setConcentration("IGF1(ds,hs,label~cold)","IGF1_cold_conc*(NA*Vecf)") # this is needed so connection is maintained

parameter_scan({suffix=>"example7_3",parameter=>"IGF1_cold_conc",par_scan_vals=>[\
		2.5822e-12,\
		6.9016e-12,\
		1.3065e-11,\
		1.3178e-12,\
		2.6498e-11,\
		6.6104e-11,\
		1.2953e-10,\
		2.5822e-10,\
		6.5536e-10,\
		1.2841e-9,\
		2.6498e-9,\
		6.7254e-9,\
		1.3178e-8,\
		2.5822e-8,\
		6.5536e-8,\
		1.2841e-7,\
		2.6498e-7,\
		6.6104e-7,\
		1.2841e-7],\
    method=>"ode",t_start=>0,t_end=>14400,n_steps=>10,print_functions=>1})

